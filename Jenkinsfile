def sendMessage(String msg, String hookUrl) {
	def payload = groovy.json.JsonOutput.toJson([text: msg])
	writeFile file: 'payload.json', text: payload

	sh(
		script: """
		export HOOK_URL=${hookUrl}
		curl -X POST -H 'Content-Type: application/json' -d @payload.json \$HOOK_URL
		""",
		label: 'Send message'
	)
}

pipeline {
	agent any

	parameters {
		choice(name: 'ENV', choices: ['dev', 'production'], description: 'Select environment')
	}

	stages {

		stage('Decide Environment') {
			steps {
				script {
					def branch = env.BRANCH_NAME ? env.BRANCH_NAME : env.GIT_BRANCH
					echo "ğŸš€ Branch: ${branch}"

					def selectedEnv = params.ENV?.trim()?.toLowerCase()

					// null ì´ê±°ë‚˜ ê³µë°±ì´ê±°ë‚˜ ì˜ëª»ëœ ê°’ì¼ ê²½ìš° ìë™ ë¶„ê¸°
					if (!selectedEnv || !(selectedEnv in ['dev', 'production'])) {
						selectedEnv = (branch == 'develop') ? 'dev' : 'production'
						echo "ğŸ”„ ENV auto-detected as: ${selectedEnv}"
					} else {
						echo "âœ… ENV manually selected: ${selectedEnv}"
					}

					env.ENV = selectedEnv
				}
			}
		}

		stage('Check ENV Credential Files') {
			steps {
				script {

					def checkCredential = { filePath, name ->
		                if (!fileExists(filePath)) {
		                    error "âŒ Credential ${name} (${filePath}) is missing."
		                } else {
		                    echo "âœ… Credential ${name} found at ${filePath}"
		                }
	            	}

					withCredentials([
						file(credentialsId: 'DB_CRED', variable: 'DB_CRED_FILE')
						]) {
                        checkCredential(DB_CRED_FILE, "DB_CRED")
					}
				}
			}
		}

		stage('Generate .env files') {
		    steps {
		        withCredentials([
		            file(credentialsId: 'DB_CRED', variable: 'DB_FILE')
		        ]) {
		            script {
		                def db = readJSON file: DB_FILE
		                def dbContent = db.collect { k, v -> "${k}=${v}" }.join('\n')
		                writeFile file: '.env', text: dbContent
		            }
		        }
		    }
		}

		stage('Set the .env value per brancah')
		{
			steps {
				script {
					def db = readProperties file: '.env'

					def isDev = (env.ENV == 'dev')

					def mySQLDbName = isDev ? db.MYSQL_DEV_DATABASE : db.MYSQL_DATABASE
					def mongoDbName = isDev ? 'nift_dev' : 'nift'

					//ë®ì–´ì“°ê¸°
					db["MYSQL_DATABASE"] = mySQLDbName
					db["MONGO_INITDB_DATABASE"] = mongoDbName

					//Spring datasource URL DBëª… ì¹˜í™˜
					db["SPRING_DATASOURCE_URL"] = db["SPRING_DATASOURCE_URL"]
					.replaceAll(/\/[^\/?]+\?/, "/${mySQLDbName}?")

					//Spring mongo URIë„ ì¹˜í™˜
					db["SPRING_DATA_MONGODB_URI"] = db["SPRING_DATA_MONGODB_URI"]
					.replaceAll(/\/[^\/?]+$/, "/${mongoDbName}")

					//ë°”ê¾¼ ê°’ë“¤ì„ ë°˜ì˜í•œ .env íŒŒì¼ ìƒì„±
					def dbContent = db.collect { k, v -> "${k}=${v}"}.join('\n')
					writeFile file: '.env', text: dbContent

					sh 'cp .env ./frontend/.env'
					sh 'cp .env ./admin/.env'

				}
			}
		}

		stage('Flyway Check and Migration') {
		    steps {
		        script {
		            if (env.ENV == 'dev') {
		                def props = readProperties file: '.env'
		                def workspace = env.WORKSPACE.replaceFirst("^/var/jenkins_home", "/home/ubuntu/jenkins-data")
		                def migrationPath = "${workspace}/backend/src/main/resources/db/migration"
		                echo "Migration Path: ${migrationPath}"

		                def baseCmd = """
		                    docker run --rm \\
		                      --network shared_backend \\
		                      -v ${migrationPath}:/flyway/sql \\
		                      flyway/flyway \\
		                      -locations=filesystem:/flyway/sql \\
		                      -url='jdbc:mysql://mysql:3306/${props.MYSQL_DATABASE}?allowPublicKeyRetrieval=true&useSSL=false' \\
		                      -user=${props.MYSQL_USER} \\
		                      -password=${props.MYSQL_PASSWORD}
		                """.stripIndent().trim()

		                // ì´ˆê¸° info ì‹œë„
		                def infoOutput = sh(
		                    script: "${baseCmd} info -outputType=json 2>&1 || true",
		                    returnStdout: true
		                ).trim()

		                def infoJson
		                try {
		                    infoJson = readJSON text: infoOutput
		                } catch (e) {
		                    if (infoOutput.contains("Detected failed migration") || infoOutput.contains("Validate failed")) {
		                        echo "ğŸ› ï¸ Validate ì‹¤íŒ¨ ê°ì§€ â†’ repair ì‹œë„"
		                        sh "${baseCmd} repair"
		                        infoOutput = sh(script: "${baseCmd} info -outputType=json", returnStdout: true).trim()
		                        infoJson = readJSON text: infoOutput
		                    } else {
		                        error "âŒ Flyway info ì‹¤íŒ¨: repairë¡œë„ ë³µêµ¬í•  ìˆ˜ ì—†ëŠ” ë¬¸ì œ\n${infoOutput}"
		                    }
		                }

		                // ìƒíƒœ í™•ì¸
		                echo "ğŸ“¦ Flyway info ìƒíƒœ:\n${infoOutput}"

						def needsRepair = infoJson?.migrations?.any {
							it.state.toLowerCase() in ['failed', 'missing_success', 'outdated', 'ignored']
						} ?: false

		                if (needsRepair) {
		                    echo "âš ï¸ Flyway ìƒíƒœ ì´ìƒ ê°ì§€ â†’ repair + migrate ì‹¤í–‰"
		                    sh "${baseCmd} repair"
		                }

		                sh "${baseCmd} migrate"
		            } else {
		                echo "ğŸ‘Œ (master branch) Skipping Flyway Migration."
		            }
		        }
		    }
		}


		stage('Run Docker Compose') {
			steps {
				script {
					try {
						def composeFile = (env.ENV == 'production') ? 'docker-compose-production.yml' : 'docker-compose-dev.yml'
						sh "docker-compose -f ${composeFile} --env-file .env up -d --build"	
						env.IMAGE_BUILD_SUCCESS = "true"
					}
					catch(Exception e) {
						env.IMAGE_BUILD_SUCCESS = "false"
						currentBuild.result = 'FAILURE'
						echo"âŒ Docker ì´ë¯¸ì§€ ìƒì„± ì‹¤íŒ¨"
					}
					
				}
			}
		}

	}

	post {
	    always {
	        script {
	            try {
	                if (env.IMAGE_BUILD_SUCCESS == "true") {

						def message = """
						*Static Analysis Report*
						- Job: ${env.JOB_NAME}
						- Build: #${env.BUILD_NUMBER}
						- íˆ´ë³„ ê²°ê³¼:
						""".stripIndent()

	                    withCredentials([string(credentialsId: 'MATTERMOST_WEBHOOK', variable: 'MATTERMOST_WEBHOOK')]){
		                    sendMessage(message, MATTERMOST_WEBHOOK)
	                    }
	                    
	                } else {
	                    def message = """
	                    âŒ *Docker ì´ë¯¸ì§€ ìƒì„± ì‹¤íŒ¨*
	                    - Job: ${env.JOB_NAME}
	                    - Build: #${env.BUILD_NUMBER}
	                    - [Jenkins ë¡œê·¸ ë³´ê¸°](${env.BUILD_URL})
	                    """.stripIndent()
	                    
	                    withCredentials([string(credentialsId: 'MATTERMOST_WEBHOOK', variable: 'MATTERMOST_WEBHOOK')]){
		                    sendMessage(message, MATTERMOST_WEBHOOK)
	                    }
	                }
	                
	                 // .env íŒŒì¼ ì‚­ì œ
                	sh 'rm -f .env.*'
	            } catch (e) {
	                echo "recordIssues() ì¤‘ ì˜¤ë¥˜ ë°œìƒ: ${e}"
	            }
	        }
	    }


		success {
			script {
				if (env.ENV == 'production') {
					echo 'âœ… Build succeeded, tagging as stable...'
					sh '''
						docker tag backend backend:stable
						docker tag frontend frontend:stable
						docker push backend:stable
						docker push frontend:stable
					'''
				}
			}
		}

		failure {
			script {
				if (env.ENV == 'production') {
					echo 'â— Build failed. Rolling back to stable image...'
					sh '''
						docker stop backend || true
						docker stop frontend || true
						docker rm backend || true
						docker rm frontend || true
						docker pull backend:stable
						docker pull frontend:stable
						docker run -d --name backend --network backend-tier -p 8081:8081 backend:stable
						docker run -d --name frontend --network frontend-tier -p 3000:3000 frontend:stable
					'''
				}
			}
		}
	}
}
